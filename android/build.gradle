/*
    SPDX-FileCopyrightText: 2021-2023 Javier O. Cordero PÃ©rez <javiercorderoperez@gmail.com>
    SPDX-FileCopyrightText: 2018-2020 Volker Krause <vkrause@kde.org>
    SPDX-FileCopyrightText: 2019 Nicolas Fella <nicolas.fella@gmx.de>
    SPDX-FileCopyrightText: 2020 Gabriel Souza Franco <gabrielfrancosouza@gmail.com>
    SPDX-License-Identifier: BSD-3-Clause
*/

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        /* classpath 'com.android.tools.build:gradle:7.0.2' */
        classpath 'com.android.tools.build:gradle:5.6.4'
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.application'
apply from: '../version.gradle'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
}

def timestamp = (int)(new Date().getTime()/1000)

android {
    /*******************************************************
     * The following variables:
     * - androidBuildToolsVersion,
     * - androidCompileSdkVersion
     * - qt5AndroidDir - holds the path to qt android files
     *                   needed to build any Qt application
     *                   on Android.
     *
     * are defined in gradle.properties file. This file is
     * updated by QtCreator and androiddeployqt tools.
     * Changing them manually might break the compilation!
     *******************************************************/

    compileSdkVersion androidCompileSdkVersion.toInteger()

    buildToolsVersion androidBuildToolsVersion

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [qt5AndroidDir + '/src', 'src', 'java']
            aidl.srcDirs = [qt5AndroidDir + '/src', 'src', 'aidl']
            res.srcDirs = [qt5AndroidDir + '/res', 'res']
            resources.srcDirs = ['resources']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
       }
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    // Do not compress Qt binary resources file
    aaptOptions {
        noCompress 'rcc'
        ignoreAssetsPattern '!<dir>ECM:!<dir>aclocal:!<dir>doc'
    }

    defaultConfig {
        resConfig "en"
        minSdkVersion = qtMinSdkVersion
        targetSdkVersion = qtTargetSdkVersion
        applicationId "com.cuperino.qprompt"
        namespace "com.cuperino.qprompt"
        versionCode timestamp
        versionName projectVersionFull
        manifestPlaceholders = [versionName: projectVersionFull, versionCode: timestamp]
    }

    packagingOptions {
        exclude 'lib/*/*RemoteObjects*'
        exclude 'lib/*/*_imageformats_qico_*'
        exclude 'lib/*/*_imageformats_qicns_*'
        exclude 'lib/*/*_imageformats_qtga_*'
        exclude 'lib/*/*_imageformats_qtiff_*'
        exclude 'lib/*/*_qmltooling_*'
    }
}
